```
Author: Tayvion P (@tayontech
Description: This alert identifies a potential bypass of repository governance controls. It triggers when a GitHub repository's default branch is changed and then reverted or updated again within a 24-hour window.  By temporarily changing the default branch to an unprotected branch, an actor can push code or merge pull requests to the primary branch (e.g., main) without satisfying mandatory status checks or peer review requirements.
```

index=github action="repo.update_default_branch"
| sort 0 repo created_at
| streamstats current=f
    last(created_at) as prev_created_at
    last(changes.default_branch) as prev_default_branch
    last(changes.old_default_branch) as prev_old_default_branch
    last(actor) as prev_actor
    last(programmatic_access_type) as prev_programmatic_access_type
    last(request_id) as prev_request_id
  by repo
| where isnotnull(prev_created_at)
| eval window_start=prev_created_at, window_end=created_at
| eval delta_ms=window_end-window_start
| where delta_ms<=86400000
| stats
    min(window_start) as initial_detection_time
    max(window_end) as last_change
    count as change_pairs
    values(prev_old_default_branch) as old_default_branch
    values(prev_default_branch) as interim_default_branch
    values(changes.default_branch) as final_default_branch
    values(prev_actor) as first_actor
    values(actor) as second_actor
    values(prev_programmatic_access_type) as first_programmatic_access_type
    values(programmatic_access_type) as second_programmatic_access_type
    values(prev_request_id) as first_request_id
    values(request_id) as second_request_id
  by repo

| eval initial_detection_time = strftime(initial_detection_time/1000, "%F %H:%M:%S %Z"),
       last_change = strftime(last_change/1000, "%F %H:%M:%S %Z")