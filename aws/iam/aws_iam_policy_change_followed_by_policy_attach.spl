```
author: Tayvion P. (@tayontech)

description: 
Detects a sequence of events where an AWS IAM Policy is modified or created and then immediately attached to a principal within a short time window. 
This behavior is highly suspicious and often indicates a rapid privilege escalation attempt or a lateral movement step where an attacker grants 
themselves new permissions for immediate use.

some fields may need to be changed to represent your current environment


tags:
  - attack.privilege_escalation
  - attack.defense_evasion
  - attack.t1098 
  - attack.t1562.001 
```
index=cloudtrail sourcetype=aws:cloudtrail eventSource="iam.amazonaws.com"
  (
    eventName IN (
      "CreatePolicy",
      "CreatePolicyVersion",
      "SetDefaultPolicyVersion",
      "DeletePolicyVersion",
      "PutUserPolicy",
      "PutRolePolicy",
      "PutGroupPolicy"
    )
    OR
    eventName IN (
      "AttachUserPolicy",
      "AttachRolePolicy",
      "AttachGroupPolicy"
    )
  )
  | eval action_type = case(
      eventName IN (
        "CreatePolicy",
        "CreatePolicyVersion",
        "SetDefaultPolicyVersion",
        "DeletePolicyVersion",
        "PutUserPolicy",
        "PutRolePolicy",
        "PutGroupPolicy"
      ), "policy_change",
      eventName IN (
        "AttachUserPolicy",
        "AttachRolePolicy",
        "AttachGroupPolicy"
      ), "policy_attach",
      true(), "other"
  )
  | eval target_principal = coalesce('requestParameters.userName', 'requestParameters.roleName', 'requestParameters.groupName'),
         policy_arn = coalesce('requestParameters.policyArn', 'responseElements.policy.arn')

  | sort 0 aws_account_id userIdentity.arn policy_arn _time

  | streamstats
      current=f
      last(action_type) as prev_action_type
      last(_time)       as prev_action_time
      by aws_account_id, userIdentity.arn, policy_arn

  | eval seconds_since_prev = _time - prev_action_time

  | where
      action_type      = "policy_attach"
      AND prev_action_type = "policy_change"
      AND seconds_since_prev <= 600
  | stats
      min(_time)             as initial_detection_time
      values(eventName)      as eventName
      values(sourceIPAddress) as src_ip
      values(target_principal) as target_principal
      values(userAgent)      as user_agent
      values(aws_account_id) as cloud_accounts
      values(awsRegion)      as aws_regions
    by userIdentity.arn

  | eval initial_detection_time = strftime(initial_detection_time, "%F %H:%M:%S %Z")
