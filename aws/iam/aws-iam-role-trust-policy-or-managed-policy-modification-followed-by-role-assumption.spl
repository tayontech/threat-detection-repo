```
author: Tayvion P. (@tayontech)

description: 
Detects a sequence of events where an AWS IAM Role Trust Policy or a Role's managed policy 
is modified (UpdateAssumeRolePolicy, PutRolePolicy) and is immediately followed by a successful 
assumption of that role (AssumeRole, AssumeRoleWithSAML, etc.) within a 10-minute window. 
This behavior is characteristic of an attacker setting up a backdoor or escalating privileges 
by granting a new, unauthorized principal (user, service, or account) permission to assume a critical role

some fields may need to be changed to represent your current environment
tags:
  - attack.privilege_escalation
  - attack.persistence
  - attack.t1098.001 
  - attack.t1550.001 
```

index=cloudtrail sourcetype=aws:cloudtrail eventSource IN ("iam.amazonaws.com","sts.amazonaws.com")
  eventName IN (
    "UpdateAssumeRolePolicy",
    "SetDefaultPolicyVersion",
    "PutRolePolicy",
    "AssumeRole",
    "AssumeRoleWithWebIdentity",
    "AssumeRoleWithSAML"
  )

  | eval action_type = case(
      eventName IN ("UpdateAssumeRolePolicy","SetDefaultPolicyVersion","PutRolePolicy"), "role_trust_change",
      eventName IN ("AssumeRole","AssumeRoleWithWebIdentity","AssumeRoleWithSAML"), "role_assume",
      true(), "other"
  )

  | eval role_name = coalesce(
      'requestParameters.roleName',
      mvindex(split('requestParameters.roleArn', "/"), -1)
  )
  | where isnotnull(role_name)

  | eval invoked_by = 'userIdentity.invokedBy'
  | eval ui_type = 'userIdentity.type'

  | eval is_automation_flag = if(
      invoked_by IN ("cloudformation.amazonaws.com","eks.amazonaws.com","sts.amazonaws.com","ssm.amazonaws.com","config.amazonaws.com")
      OR ui_type="AWSService",
      1, 0
  )
  | eval is_automation = if(is_automation_flag=1,"true","false")

  | eval trust_policy = if(eventName="UpdateAssumeRolePolicy", 'requestParameters.policyDocument', null())

  | eventstats max(eval(action_type="role_trust_change")) as has_trust_change
      by aws_account_id, role_name, userIdentity.arn
  | where has_trust_change=1

  | sort aws_account_id role_name userIdentity.arn _time

  | streamstats current=f
      last(action_type)        as prev_action_type
      last(_time)              as prev_action_time
      last(is_automation_flag) as prev_is_automation_flag
      last(invoked_by)         as prev_invoked_by
      last(ui_type)            as prev_ui_type
      last(trust_policy)       as prev_trust_policy
    by aws_account_id, role_name, userIdentity.arn

  | eval seconds_since_prev = _time - prev_action_time

  | where action_type="role_assume"
    AND prev_action_type="role_trust_change"
    AND seconds_since_prev<=600

  | where NOT (
      prev_is_automation_flag=1 AND is_automation_flag=1
      AND (
        invoked_by IN ("cloudformation.amazonaws.com","eks.amazonaws.com","sts.amazonaws.com","ssm.amazonaws.com","config.amazonaws.com")
        OR ui_type="AWSService"
      )
      AND (
        prev_invoked_by IN ("cloudformation.amazonaws.com","eks.amazonaws.com","sts.amazonaws.com","ssm.amazonaws.com","config.amazonaws.com")
        OR prev_ui_type="AWSService"
      )
  )

  | stats
      min(_time)                   as initial_detection_time
      values(sourceIPAddress)      as source_ip
      values(role_name)            as roles_assumed
      values(prev_trust_policy)    as trust_policy_modified
      values(userAgent)            as user_agent
      values(aws_account_id)       as cloud_account
      values(is_automation)        as is_automation
    by userIdentity.arn

  | eval initial_detection_time = strftime(initial_detection_time,"%F %T %Z")
