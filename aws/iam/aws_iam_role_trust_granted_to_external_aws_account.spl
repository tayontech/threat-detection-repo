```
author: Tayvion P. (@tayontech)

description: 
Detects the creation or modification of an IAM Role trust policy that grants  
assume-role permissions to an AWS account that is not part of the organization. 
This action creates a backdoor for an external entity to gain persistent access 
to your environment, often used by attackers to maintain unauthorized access. 

inputlookup table must be established, otherwise you will have to filter aws accounts manually
some fields may need to be changed to represent your current environment


tags:
  - attack.persistence
  - attack.defense_evasion
  - attack.t1199 
  - attack.t1133 
```
index=cloudtrail sourcetype=aws:cloudtrail eventSource="iam.amazonaws.com" eventName IN ("CreateRole","UpdateAssumeRolePolicy")
  | eval assume_doc = coalesce(
      'requestParameters.assumeRolePolicyDocument',
      'requestParameters.policyDocument'
  )
  | eval assume_doc = urldecode(assume_doc)
  | rex field=assume_doc max_match=0 "\"arn:aws:iam::(?<principal_account_ids>\d{12})"
  | mvexpand principal_account_ids
  | where isnotnull(principal_account_ids)
  | where principal_account_ids != aws_account_id
  | lookup aws_org_accounts.csv account_id as principal_account_ids OUTPUT account_id as org_account_id
  | where isnull(org_account_id)

  | stats
      min(_time)                        as initial_detection_time
      values(sourceIPAddress)           as src_ip
      values(eventName)                 as eventName
      values(assume_doc)                as assumed_policies
      values(aws_account_id)            as cloudaccount
      values(principal_account_ids)     as external_account
      values(awsRegion)                 as awsregion
    by user_arn,requestParameters.roleName
  | where isnotnull(external_account) AND external_account!=""
  | rename requestParameters.roleName as role_assumed
  | eval initial_detection_time = strftime(initial_detection_time,"%F %T %Z")
  