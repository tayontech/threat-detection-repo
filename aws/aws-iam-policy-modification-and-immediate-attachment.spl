```
author: Tayvion P. (@tayontech)

description: 
Detects a sequence of events where an AWS IAM Policy is modified or created and then immediately attached to a principal within a short time window. 
This behavior is highly suspicious and often indicates a rapid privilege escalation attempt or a lateral movement step where an attacker grants 
themselves new permissions for immediate use.

some fields may need to be changed to represent your current environment


tags:
  - attack.privilege_escalation
  - attack.defense_evasion
  - attack.t1098 
  - attack.t1562.001 
```
index=cloudtrail sourcetype=aws:cloudtrail eventSource="iam.amazonaws.com"
  eventName IN (
    "CreatePolicy","CreatePolicyVersion","SetDefaultPolicyVersion","DeletePolicyVersion",
    "AttachUserPolicy","AttachRolePolicy","AttachGroupPolicy"
  )
  | eval action_type = case(
      eventName IN ("CreatePolicy","CreatePolicyVersion","SetDefaultPolicyVersion","DeletePolicyVersion"), "policy_change",
      eventName IN ("AttachUserPolicy","AttachRolePolicy","AttachGroupPolicy"), "policy_attach",
      true(), "other"
  )

  | eval policy_arn = coalesce('requestParameters.policyArn','responseElements.policy.arn')
  | where isnotnull(policy_arn)

  | eval target_principal = coalesce('requestParameters.userName','requestParameters.roleName','requestParameters.groupName')

  | eval ui_type = 'userIdentity.type'
  | eval invoked_by = 'userIdentity.invokedBy'

  | eval is_automation_flag = if(
      invoked_by IN ("cloudformation.amazonaws.com","autoscaling.amazonaws.com","eks.amazonaws.com","ssm.amazonaws.com","config.amazonaws.com")
      OR ui_type="AWSService"
      OR match(user_arn, "(?i)terraform|pulumi|cloudformation|cicd|pipeline|github|jenkins")
      OR match(userAgent, "(?i)\\bterraform\\b|\\bpulumi\\b|\\bcloudformation\\b|\\bjenkins\\b|\\bgitlab\\b|\\bcircleci\\b|\\bgithub\\b|\\bactions\\b"),
      1, 0
  )
  | eval is_automation = if(is_automation_flag=1, "true", "false")

  | sort 0 aws_account_id policy_arn user_arn _time
  | streamstats current=f
      last(action_type)         as prev_action_type
      last(_time)               as prev_action_time
      last(is_automation_flag)  as prev_is_automation_flag
      last(invoked_by)          as prev_invoked_by
      last(ui_type)             as prev_ui_type
    by aws_account_id, policy_arn, user_arn

  | eval seconds_since_prev = _time - prev_action_time

  | where action_type="policy_attach"
    AND prev_action_type="policy_change"
    AND seconds_since_prev<=600

  | where NOT (
      prev_is_automation_flag=1 AND is_automation_flag=1
      AND (
        invoked_by IN ("cloudformation.amazonaws.com","autoscaling.amazonaws.com","eks.amazonaws.com","ssm.amazonaws.com","config.amazonaws.com")
        OR ui_type="AWSService"
      )
      AND (
        prev_invoked_by IN ("cloudformation.amazonaws.com","autoscaling.amazonaws.com","eks.amazonaws.com","ssm.amazonaws.com","config.amazonaws.com")
        OR prev_ui_type="AWSService"
      )
  )

  | stats
      min(_time)               as initial_detection_time
      values(eventName)        as actions
      values(policy_arn)       as policy_arn
      values(target_principal) as target_principal
      values(sourceIPAddress)  as source_ip
      values(userAgent)        as user_agent
      values(awsRegion)        as aws_region
      values(ui_type)          as identity_type
      values(invoked_by)       as invoked_by
      values(is_automation)    as is_automation
    by aws_account_id, user_arn

  | eval initial_detection_time = strftime(initial_detection_time, "%F %T %Z")